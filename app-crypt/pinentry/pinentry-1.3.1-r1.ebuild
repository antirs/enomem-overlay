# Copyright 1999-2025 Gentoo Authors
# Distributed under the terms of the GNU General Public License v2

EAPI=8

VERIFY_SIG_OPENPGP_KEY_PATH=/usr/share/openpgp-keys/gnupg.asc
inherit autotools flag-o-matic qmake-utils verify-sig

DESCRIPTION="Simple passphrase entry dialogs which utilize the Assuan protocol"
HOMEPAGE="https://gnupg.org/related_software/pinentry/"
SRC_URI="mirror://gnupg/${PN}/${P}.tar.bz2"
SRC_URI+=" verify-sig? ( mirror://gnupg/${PN}/${P}.tar.bz2.sig )"

LICENSE="GPL-2"
SLOT="0"
KEYWORDS="~alpha amd64 arm arm64 hppa ~loong ~m68k ~mips ppc ppc64 ~riscv ~s390 sparc x86 ~amd64-linux ~x86-linux ~arm64-macos ~ppc-macos ~x64-macos ~x64-solaris"
IUSE="caps efl emacs gtk keyring ncurses qt6 static static-libs wayland X"

REQUIRED_USE="
	static? ( !efl )
	static? ( !gtk )
	static? ( !keyring )
	static? ( !qt6 )
	static? ( !wayland )
	static? ( !X )
"

LIB_DEPEND="
	>=dev-libs/libassuan-2.1:=[static-libs?]
	>=dev-libs/libgcrypt-1.6.3[static-libs?]
	>=dev-libs/libgpg-error-1.17[static?,static-libs?]
	efl? ( dev-libs/efl[X] )
	keyring? ( app-crypt/libsecret )
	ncurses? ( sys-libs/ncurses:=[static-libs?] )
	qt6? (
		dev-qt/qtbase:6[gui,widgets]
		wayland? (
			kde-frameworks/kguiaddons:6
			kde-frameworks/kwindowsystem:6
		)
	)
"
RDEPEND="
	!static? ( ${LIB_DEPEND} )
	gtk? ( app-crypt/gcr:4[gtk] )
"
DEPEND="
	${RDEPEND}
	${LIB_DEPEND}
"
BDEPEND="
	sys-devel/gettext
	virtual/pkgconfig
	verify-sig? ( sec-keys/openpgp-keys-gnupg )
"
PDEPEND="
	emacs? ( app-emacs/pinentry )
	static? ( >=dev-libs/libgpg-error-1.17[static] )
"
IDEPEND=">=app-eselect/eselect-pinentry-0.7.4"

DOCS=( AUTHORS ChangeLog NEWS README THANKS TODO )

PATCHES=(
	"${FILESDIR}/${PN}-1.0.0-AR.patch"
	"${FILESDIR}/${PN}-1.3.0-automagic.patch" # bug #819939, bug #837719
)

src_prepare() {
	default

	eautoreconf
}

src_configure() {
	unset FLTK_CONFIG

	local myeconfargs=(
		$(use_enable efl pinentry-efl)
		$(use_enable emacs pinentry-emacs)
		$(use_enable keyring libsecret)
		$(use_enable gtk pinentry-gnome3)
		$(use_enable ncurses fallback-curses)
		$(use_enable ncurses pinentry-curses)
		$(use_enable qt6 pinentry-qt)
		$(use_with X x)

		--enable-pinentry-tty
		--disable-kf5-wayland
		--disable-pinentry-fltk
		--disable-pinentry-gtk2
		--disable-pinentry-qt5
		--disable-qtx11extras

		ac_cv_path_GPGRT_CONFIG="${ESYSROOT}/usr/bin/${CHOST}-gpgrt-config"

		$("${S}/configure" --help | grep -- '--without-.*-prefix' | sed -e 's/^ *\([^ ]*\) .*/\1/g')
	)

	if use qt6 ; then
		export PATH="$(qt6_get_bindir):${PATH}"
		export QTLIB="$(qt6_get_libdir):${QTLIB}"
		export MOC="$(qt6_get_libdir)/qt6/libexec/moc"

		myeconfargs+=(
			$(use_enable wayland kf6-wayland)
		)
	else
		myeconfargs+=(
			--disable-kf6-wayland
		)
	fi

	use static && append-ldflags -static $(test-flags-CCLD --static)

	if [[ -n "${ESYSROOT}" ]]; then
		append-ldflags -L"${ESYSROOT}"/usr/$(get_libdir)
		export PKG_CONFIG_PATH="${ESYSROOT}/usr/$(get_libdir)/pkgconfig"
		export PKG_CONFIG_SYSROOT_DIR="${ESYSROOT}"
	fi

	if use static; then
		PKG_CONFIG="$(tc-getPKG_CONFIG) --static" econf "${myeconfargs[@]}"
	else
		econf "${myeconfargs[@]}"
	fi
}

src_install() {
	default

	rm "${ED}"/usr/bin/pinentry || die

	# The preferred Qt implementation upstream gets installed as just 'qt'.
	# Make a symlink for eselect-pinentry and friends.
	if use qt6 ; then
		dosym pinentry-qt /usr/bin/pinentry-qt6
	fi
}

pkg_postinst() {
	eselect pinentry update ifunset
}

pkg_postrm() {
	eselect pinentry update ifunset
}
